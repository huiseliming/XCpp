if(NOT EXISTS "${CMAKE_BINARY_DIR}/compile_flags.txt")
    write_file(${CMAKE_BINARY_DIR}/compile_flags.txt "-xc++")
endif()
macro(cpp_header_tool CPP_MODULE_NAME)
    set(${CPP_MODULE_NAME}_HEADERS_ALL_CPP ${CMAKE_BINARY_DIR}/${CPP_MODULE_NAME}.all.cpp)
    file(WRITE ${${CPP_MODULE_NAME}_HEADERS_ALL_CPP} "")
    foreach(HeaderFile ${${CPP_MODULE_NAME}_HEADERS})
        file(APPEND ${${CPP_MODULE_NAME}_HEADERS_ALL_CPP} "#include \"${HeaderFile}\"\n")
    endforeach()

    set(${CPP_MODULE_NAME}_GENERATED_HEADERS "")
    set(${CPP_MODULE_NAME}_GENERATED_SOURCES "")
    foreach(HEADER_FILE ${${CPP_MODULE_NAME}_HEADERS})
        file(RELATIVE_PATH HEADER_FILE_RELATIVE_PATH ${CMAKE_SOURCE_DIR} ${HEADER_FILE})
        string(REGEX REPLACE "\\.[^.]*$" "" GENERATED_RELATIVE_PATH ${HEADER_FILE_RELATIVE_PATH})
        list(APPEND ${CPP_MODULE_NAME}_GENERATED_HEADERS "${CMAKE_BINARY_DIR}/${GENERATED_RELATIVE_PATH}.gen.h")
        list(APPEND ${CPP_MODULE_NAME}_GENERATED_SOURCES "${CMAKE_BINARY_DIR}/${GENERATED_RELATIVE_PATH}.gen.cpp")
    endforeach()
    list(JOIN ${CPP_MODULE_NAME}_HEADERS "\" --target=\"" EXTRA_ARG_HEADERS)
    add_custom_target(${CPP_MODULE_NAME}_GeneratedFile
        COMMAND ${CPP_KIT_HEADER_TOOL_EXE} 
        ${${CPP_MODULE_NAME}_HEADERS_ALL_CPP} 
        --src_dir=${CMAKE_SOURCE_DIR} 
        --gen_dir=${CMAKE_BINARY_DIR} 
        --target="${EXTRA_ARG_HEADERS}" 
        -- clang++ -Xclang -std=c++20 -fexceptions -Wno-everything -D__EXECUTE_CPP_KIT_HEADER_TOOL__ 
        "-I\"$<JOIN:$<TARGET_PROPERTY:${CPP_MODULE_NAME},INCLUDE_DIRECTORIES>,\" -I\">\""
        "-include\"$<JOIN:$<TARGET_PROPERTY:${CPP_MODULE_NAME},PRECOMPILE_HEADERS>,\" -include\">\""
        "-D$<JOIN:$<TARGET_PROPERTY:${CPP_MODULE_NAME},COMPILE_DEFINITIONS>, -D>" 
        -c
        #DEPENDS ${${CPP_MODULE_NAME}_HEADERS}
        BYPRODUCTS ${${CPP_MODULE_NAME}_GENERATED_HEADERS} ${${CPP_MODULE_NAME}_GENERATED_SOURCES}
        COMMAND_EXPAND_LISTS
    )
    target_sources(${CPP_MODULE_NAME} PRIVATE ${${CPP_MODULE_NAME}_GENERATED_HEADERS} ${${CPP_MODULE_NAME}_GENERATED_SOURCES})
    source_group(
        TREE ${CMAKE_BINARY_DIR}
        PREFIX gen
        FILES ${${CPP_MODULE_NAME}_GENERATED_HEADERS} ${${CPP_MODULE_NAME}_GENERATED_SOURCES}
    )
    #set_property(TARGET ${CPP_MODULE_NAME} APPEND PROPERTY ADDITIONAL_CLEAN_FILES ${${CPP_MODULE_NAME}_GENERATED_HEADERS} ${${CPP_MODULE_NAME}_GENERATED_SOURCES})
    add_dependencies(${CPP_MODULE_NAME} ${CPP_MODULE_NAME}_GeneratedFile)
endmacro()

